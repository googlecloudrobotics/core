load("@io_bazel_rules_go//go:def.bzl", "go_binary", "go_library")
load("@rules_oci//oci:defs.bzl", "oci_image")
load("@rules_pkg//pkg:tar.bzl", "pkg_tar")

package(default_visibility = ["//visibility:public"])

go_library(
    name = "go_default_library",
    srcs = ["main.go"],
    importpath = "github.com/googlecloudrobotics/core/src/go/cmd/chart-assignment-controller",
    visibility = ["//visibility:private"],
    deps = [
        "//src/go/pkg/apis/apps/v1alpha1:go_default_library",
        "//src/go/pkg/controller/chartassignment:go_default_library",
        "@com_github_googlecloudrobotics_ilog//:go_default_library",
        "@com_github_pkg_errors//:go_default_library",
        "@io_k8s_apimachinery//pkg/runtime:go_default_library",
        "@io_k8s_client_go//kubernetes/scheme:go_default_library",
        "@io_k8s_client_go//rest:go_default_library",
        "@io_k8s_sigs_controller_runtime//pkg/healthz:go_default_library",
        "@io_k8s_sigs_controller_runtime//pkg/log:go_default_library",
        "@io_k8s_sigs_controller_runtime//pkg/log/zap:go_default_library",
        "@io_k8s_sigs_controller_runtime//pkg/manager:go_default_library",
        "@io_k8s_sigs_controller_runtime//pkg/manager/signals:go_default_library",
        "@io_k8s_sigs_controller_runtime//pkg/metrics/server:go_default_library",
        "@io_k8s_sigs_controller_runtime//pkg/webhook:go_default_library",
        "@io_opencensus_go//trace:go_default_library",
        "@io_opencensus_go_contrib_exporter_stackdriver//:go_default_library",
    ],
)

genrule(
    name = "helm-config",
    outs = ["helm-config.tar"],
    # To make the rule deterministic
    #  - Remove the "generated" timestamp in repositories.yaml.
    #  - Add some extra tar flags.
    cmd = "HELM_HOME=$$PWD/.helm $(location @kubernetes_helm//:helm) >/dev/null init --client-only --skip-repos " +
          "&& sed -i '/generated:/d' .helm/repository/repositories.yaml " +
          "&& tar --owner=root --group=root --numeric-owner --mtime='2010-01-01' --create --file $@ .helm",
    output_to_bindir = True,
    tools = ["@kubernetes_helm//:helm"],
)

oci_image(
    name = "helm-image",
    base = "@distroless_cc",
    tars = [":helm-image-layer"],
)

pkg_tar(
    name = "helm-image-layer",
    extension = "tar.gz",
    package_dir = "/home/nonroot/",
    deps = [":helm-config"],
)

go_binary(
    name = "chart-assignment-controller-app",
    embed = [":go_default_library"],
)

pkg_tar(
    name = "chart-assignment-controller-layer",
    srcs = [":chart-assignment-controller-app"],
    extension = "tar.gz",
)

oci_image(
    name = "chart-assignment-controller-image",
    base = ":helm-image",
    entrypoint = ["/chart-assignment-controller-app"],
    tars = [":chart-assignment-controller-layer"],
)
